// Copyright Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	GpuSkinVertexFactory.hlsl: GPU skinned vertex factory shader code
=============================================================================*/


#include "VertexFactoryCommon.ush"
#include "GpuSkinCommon.ush"

#ifndef GPUSKIN_MORPH_BLEND
#define GPUSKIN_MORPH_BLEND 0
#endif

#ifndef GPUSKIN_APEX_CLOTH
#define GPUSKIN_APEX_CLOTH 0
#endif

#ifndef GPUSKIN_LIMIT_2BONE_INFLUENCES
#define GPUSKIN_LIMIT_2BONE_INFLUENCES	0 // default is 4 bones
#endif

#ifndef GPUSKIN_UNLIMITED_BONE_INFLUENCE
#define GPUSKIN_UNLIMITED_BONE_INFLUENCE 0
#endif

#ifndef GPUSKIN_USE_BONES_SRV_BUFFER
#define GPUSKIN_USE_BONES_SRV_BUFFER	0	// Use a Buffer<float4> if 1, otherwise a uniform buffer  
#endif

#ifndef GPUSKIN_MULTIPLE_CLOTH_INFLUENCES
#define GPUSKIN_MULTIPLE_CLOTH_INFLUENCES 0
#endif

#define MAX_INFLUENCES_PER_STREAM	4

#define FIXED_VERTEX_INDEX 0xFFFF

// if the per bone motion blur is enabled for this draw call
uint PerBoneMotionBlur;
// number of influences for this draw call, 4 or 8
uint NumBoneInfluencesParam;

#if GPUSKIN_APEX_CLOTH
/** Vertex buffer from which to read simulated positions of clothing. */
#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
Buffer<float2> ClothSimulVertsPositionsNormals;
Buffer<float2> PreviousClothSimulVertsPositionsNormals;
/** Transform to apply when positions/normals are loaded */
float4x4 ClothLocalToWorld;
float4x4 PreviousClothLocalToWorld;
#endif // #if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
/** blend weight between simulated positions and original key-framed animation */
float ClothBlendWeight;
#endif// #if GPUSKIN_APEX_CLOTH

#if GPUSKIN_UNLIMITED_BONE_INFLUENCE
uint InputWeightIndexSize; // bone index size: 1 - 8bit or 2 - 16bit
Buffer<uint> InputWeightStream;
#endif

struct FVertexFactoryInput
{
	float4	Position		: ATTRIBUTE0;
	// 0..1
	half3	TangentX		: ATTRIBUTE1;
	// 0..1
	// TangentZ.w contains sign of tangent basis determinant
	half4	TangentZ		: ATTRIBUTE2;	

#if GPUSKIN_UNLIMITED_BONE_INFLUENCE
	uint	BlendOffsetCount	: ATTRIBUTE3;
#else
	uint4	BlendIndices	: ATTRIBUTE3;
	uint4	BlendIndicesExtra	: ATTRIBUTE14;
	float4	BlendWeights	: ATTRIBUTE4;	
	float4	BlendWeightsExtra	: ATTRIBUTE15;
#endif // GPUSKIN_UNLIMITED_BONE_INFLUENCE


#if NUM_MATERIAL_TEXCOORDS_VERTEX
	// If this changes make sure to update LocalVertexFactory.usf
	float2	TexCoords[NUM_MATERIAL_TEXCOORDS_VERTEX] : ATTRIBUTE5;

	#if NUM_MATERIAL_TEXCOORDS_VERTEX > 4
		#error Too many texture coordinate sets defined on GPUSkin vertex input. Max: 4.
	#endif
#endif

#if GPUSKIN_MORPH_BLEND
	// NOTE: TEXCOORD6,TEXCOORD7 used instead of POSITION1,NORMAL1 since those semantics are not supported by Cg 
	/** added to the Position */
	float3	DeltaPosition	: ATTRIBUTE9;	//POSITION1;
	/** added to the TangentZ and then used to derive new TangentX,TangentY, .w contains the weight of the tangent blend */
	float3	DeltaTangentZ	: ATTRIBUTE10;	//NORMAL1;
#endif

	float3 PreSkinOffset : ATTRIBUTE11;
	float3 PostSkinOffset : ATTRIBUTE12;

#if GPUSKIN_APEX_CLOTH
	uint ClothVertexID : SV_VertexID;
#endif

	/** Per vertex color */
	float4 Color : ATTRIBUTE13;
};

#if !GPUSKIN_MORPH_BLEND && GPUSKIN_APEX_CLOTH
// .x = Draw Index Buffer offset, .y = Offset into Cloth Vertex Buffer
uint2 GPUSkinApexClothStartIndexOffset;

#if GPUSKIN_MULTIPLE_CLOTH_INFLUENCES
// This must match NUM_INFLUENCES_PER_VERTEX in ClothingMeshUtils.cpp and GpuSkinCacheComputeShader.usf
// TODO: Make this easier to change in without messing things up
#define NUM_INFLUENCES_PER_VERTEX 5
#else
#define NUM_INFLUENCES_PER_VERTEX 1
#endif

struct FClothVertex
{
    // APEX cloth mesh-mesh mapping data
    // Barycentric Coordinate Data
    float4	BaryCoordPos;
	float4	BaryCoordNormal;
	float4	BaryCoordTangent;
	uint4	SimulIndices;

    float Weight;

/*
	// Contains the 3 indices for verts in the source mesh forming a triangle, the last element
	// is a flag to decide how the skinning works, 0xffff uses no simulation, and just normal
	// skinning, anything else uses the source mesh and the above skin data to get the final position
	uint16	 SourceMeshVertIndices[4];

	// Dummy for alignment (16 bytes)
	uint32	 Padding[2];
*/
};


#define NUM_FLOAT4S_PER_VERTEX_INFLUENCE 4;   // stride of GPUSkinApexCloth


//@todo Switch to StructuredBuffer<FClothVertex>
Buffer<float4> GPUSkinApexCloth;

void SetupClothVertex(uint VertexIndex, out FClothVertex ClothVertexInfluences[NUM_INFLUENCES_PER_VERTEX])
{
    VertexIndex = VertexIndex + GPUSkinApexClothStartIndexOffset.y - GPUSkinApexClothStartIndexOffset.x;

    for (int i = 0; i < NUM_INFLUENCES_PER_VERTEX; ++i )
    {
        uint Offset = (NUM_INFLUENCES_PER_VERTEX * VertexIndex + i) * NUM_FLOAT4S_PER_VERTEX_INFLUENCE;

        ClothVertexInfluences[i].BaryCoordPos = GPUSkinApexCloth[Offset];
        ClothVertexInfluences[i].BaryCoordNormal = GPUSkinApexCloth[Offset + 1];
        ClothVertexInfluences[i].BaryCoordTangent = GPUSkinApexCloth[Offset + 2];

	    uint4 PackedIndices = asuint(GPUSkinApexCloth[Offset + 3]);
        ClothVertexInfluences[i].SimulIndices.yw = (PackedIndices.xy >> 16) & 0xffff;
        ClothVertexInfluences[i].SimulIndices.xz = PackedIndices.xy & 0xffff;

        ClothVertexInfluences[i].Weight = asfloat(PackedIndices[2]);
    }
}
#endif

struct FVertexFactoryInterpolantsVSToPS
{
	TANGENTTOWORLD_INTERPOLATOR_BLOCK

#if INTERPOLATE_VERTEX_COLOR
	float4 Color : COLOR0;
#endif

#if NUM_TEX_COORD_INTERPOLATORS
	float2	TexCoords[NUM_TEX_COORD_INTERPOLATORS]	: TEXCOORD0;
#endif

#if INSTANCED_STEREO
	nointerpolation uint EyeIndex : PACKED_EYE_INDEX;
#endif
};

struct FVertexFactoryInterpolantsVSToDS
{
	FVertexFactoryInterpolantsVSToPS InterpolantsVSToPS;
};

/** Converts from vertex factory specific interpolants to a FMaterialPixelParameters, which is used by material inputs. */
FMaterialPixelParameters GetMaterialPixelParameters(FVertexFactoryInterpolantsVSToPS Interpolants, float4 SvPosition)
{
	// GetMaterialPixelParameters is responsible for fully initializing the result
	FMaterialPixelParameters Result = MakeInitializedMaterialPixelParameters();

#if NUM_TEX_COORD_INTERPOLATORS	
	UNROLL
	for(int CoordinateIndex = 0;CoordinateIndex < NUM_TEX_COORD_INTERPOLATORS;CoordinateIndex++)
	{
		Result.TexCoords[CoordinateIndex] = Interpolants.TexCoords[CoordinateIndex];
	}
#endif

	half3 TangentToWorld0 = Interpolants.TangentToWorld0.xyz;
	half4 TangentToWorld2 = Interpolants.TangentToWorld2;
	Result.TangentToWorld = AssembleTangentToWorld( TangentToWorld0, TangentToWorld2 );
#if USE_WORLDVERTEXNORMAL_CENTER_INTERPOLATION
	Result.WorldVertexNormal_Center = Interpolants.TangentToWorld2_Center.xyz;
#endif
	Result.UnMirrored = TangentToWorld2.w;
#if INTERPOLATE_VERTEX_COLOR
	Result.VertexColor = Interpolants.Color;
#else
	Result.VertexColor = 0;
#endif
	Result.TwoSidedSign = 1;
	return Result;
}

#define FBoneMatrix float3x4

#if GPUSKIN_USE_BONES_SRV_BUFFER

// The bone matrix buffer stored as 4x3 (3 float4 behind each other), all chunks of a skeletal mesh in one, each skeletal mesh has it's own buffer
STRONG_TYPE Buffer<float4> BoneMatrices;
// The previous bone matrix buffer stored as 4x3 (3 float4 behind each other), all chunks of a skeletal mesh in one, each skeletal mesh has it's own buffer
STRONG_TYPE Buffer<float4> PreviousBoneMatrices;

#endif

// Cache data to avoid multiple calculation 
struct FVertexFactoryIntermediates
{
	// Blend Matrix (used for position/tangents)
	FBoneMatrix BlendMatrix;

	// Unpacked position
	float3 UnpackedPosition;
	
	// Tangent Basis
	float3x3 TangentToLocal; 

	// Vertex Color
	float4 Color;

#if GPUSKIN_APEX_CLOTH
	// in world space (non translated)
	float3 SimulatedPosition;
	// in world space (non translated)
	float3 PreviousSimulatedPosition;
	
	FClothVertex ClothVertexInfluences[NUM_INFLUENCES_PER_VERTEX];
#endif

};

/** Converts from vertex factory specific input to a FMaterialVertexParameters, which is used by vertex shader material inputs. */
FMaterialVertexParameters GetMaterialVertexParameters(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 WorldPosition, float3x3 TangentToLocal)
{
	FMaterialVertexParameters Result = (FMaterialVertexParameters)0;
	Result.WorldPosition = WorldPosition;
	Result.VertexColor = Intermediates.Color;
	Result.TangentToWorld = mul(TangentToLocal, GetLocalToWorld3x3());
	Result.PreSkinnedPosition = Intermediates.UnpackedPosition.xyz;
	Result.PreSkinnedNormal = Input.TangentZ.xyz;

	Result.PreSkinOffset = Input.PreSkinOffset;
	Result.PostSkinOffset = Input.PostSkinOffset;

	// Assumes no instacing
	Result.PrevFrameLocalToWorld = Primitive.PreviousLocalToWorld;

#if NUM_MATERIAL_TEXCOORDS_VERTEX
	for(int CoordinateIndex = 0; CoordinateIndex < NUM_MATERIAL_TEXCOORDS_VERTEX; CoordinateIndex++)
	{
		Result.TexCoords[CoordinateIndex] = Input.TexCoords[CoordinateIndex];
	}
#endif
	return Result;
}

#if GPUSKIN_APEX_CLOTH

// if false, fixed vertex
bool IsSimulatedVertex(FClothVertex Input)
{
	return (Input.SimulIndices.w < FIXED_VERTEX_INDEX);
}

float3 GetClothSimulPosition(int Index, bool bPrevious)
{
#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
	float3 Position;
	if(bPrevious)
	{
		Position = float3(PreviousClothSimulVertsPositionsNormals[Index * 3], PreviousClothSimulVertsPositionsNormals[Index * 3 + 1].x);
		return float3(mul(float4(Position, 1), PreviousClothLocalToWorld).xyz);
	}
	else
	{
		Position = float3(ClothSimulVertsPositionsNormals[Index * 3], ClothSimulVertsPositionsNormals[Index * 3 + 1].x);
		return float3(mul(float4(Position, 1), ClothLocalToWorld).xyz);
	}
	
#endif //FEATURE_LEVEL >= FEATURE_LEVEL_SM4
}

float3 GetClothSimulNormal(int Index, bool bPrevious)
{
#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
	float3 Normal;
	if(bPrevious)
	{
		Normal = float3(PreviousClothSimulVertsPositionsNormals[Index * 3 + 1].y, PreviousClothSimulVertsPositionsNormals[Index * 3 + 2]);
		return float3(mul(float4(Normal, 0), PreviousClothLocalToWorld).xyz);
	}
	else
	{
		Normal = float3(ClothSimulVertsPositionsNormals[Index * 3 + 1].y, ClothSimulVertsPositionsNormals[Index * 3 + 2]);
		return float3(mul(float4(Normal, 0), ClothLocalToWorld).xyz);
	}
	
#endif //FEATURE_LEVEL >= FEATURE_LEVEL_SM4
}

float3 ClothingPosition(FClothVertex ClothVertexInfluences[NUM_INFLUENCES_PER_VERTEX], bool bPrevious)
{
	int NumInfluences = 0;
	float SumWeights = 0.0f;
	float3 AveragedSimulatedPosition = float3(0, 0, 0);
	
    for (int i = 0; i < NUM_INFLUENCES_PER_VERTEX; ++i )
    {
        const FClothVertex Influence = ClothVertexInfluences[i];

	    if( Influence.SimulIndices.w < 0xFFFF )
	    { 
            ++NumInfluences;

            float3 A = GetClothSimulPosition(Influence.SimulIndices.x, bPrevious);
		    float3 B = GetClothSimulPosition(Influence.SimulIndices.y, bPrevious);
		    float3 C = GetClothSimulPosition(Influence.SimulIndices.z, bPrevious);

		    float3 NA = GetClothSimulNormal(Influence.SimulIndices.x, bPrevious);
		    float3 NB = GetClothSimulNormal(Influence.SimulIndices.y, bPrevious);
		    float3 NC = GetClothSimulNormal(Influence.SimulIndices.z, bPrevious);

			float Weight = 1.0f;
			if ( NUM_INFLUENCES_PER_VERTEX > 1 )
			{
				// Weight is packed in the last coordinate
				Weight = Influence.Weight;
				SumWeights += Weight;
			}
			else
			{
				// Single influence, weight is 1.0
	            Weight = 1.0f;
                SumWeights = 1.0f;	   
			}

			float3 TriangleBary = float3(Influence.BaryCoordPos.x,
                                         Influence.BaryCoordPos.y,
										 1.0f - Influence.BaryCoordPos.x - Influence.BaryCoordPos.y);

			float3 SimPosition = TriangleBary.x * (A + NA * Influence.BaryCoordPos.w)
					           + TriangleBary.y * (B + NB * Influence.BaryCoordPos.w)
					           + TriangleBary.z * (C + NC * Influence.BaryCoordPos.w);

			AveragedSimulatedPosition += Weight * SimPosition;
	    }
    }

    if ( NumInfluences > 0 && SumWeights > 1e-4f )
    {
		float InvWeight = 1.0f / SumWeights; 
		AveragedSimulatedPosition *= InvWeight;
	}
	else
	{
		AveragedSimulatedPosition = float3(0, 0, 0);
	}

	return AveragedSimulatedPosition;
}

#endif //#if GPUSKIN_APEX_CLOTH

/**
* Unpack position - uncompress xyz position to world position
*/
float3 UnpackedPosition( FVertexFactoryInput Input )
{
		return float3(Input.Position.xyz);
}

#if GPUSKIN_MORPH_BLEND
/** 
* Adds the delta position from the combined morph targets to the vertex position 
*/
float3 MorphPosition( FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates )
{
	return Intermediates.UnpackedPosition + Input.DeltaPosition;
}
#endif

FBoneMatrix GetBoneMatrix(int Index)
{
#if GPUSKIN_USE_BONES_SRV_BUFFER
	float4 A = BoneMatrices[Index * 3];
	float4 B = BoneMatrices[Index * 3 + 1];
	float4 C = BoneMatrices[Index * 3 + 2];
	return FBoneMatrix(A,B,C);
#else
	return Bones.BoneMatrices[Index];
#endif
}

FBoneMatrix CalcBoneMatrix( FVertexFactoryInput Input )
{
#if GPUSKIN_UNLIMITED_BONE_INFLUENCE
	int NumBoneInfluences = Input.BlendOffsetCount & 0xff;
	int StreamOffset = Input.BlendOffsetCount >> 8;
	int WeightsOffset = StreamOffset + (InputWeightIndexSize * NumBoneInfluences);

	FBoneMatrix BoneMatrix = FBoneMatrix(float4(0,0,0,0), float4(0,0,0,0), float4(0,0,0,0));
	for (int InfluenceIdx = 0; InfluenceIdx < NumBoneInfluences; InfluenceIdx++)
	{
		int BoneIndexOffset = StreamOffset + (InputWeightIndexSize * InfluenceIdx);
		int BoneIndex = InputWeightStream[BoneIndexOffset];
		if (InputWeightIndexSize > 1)
		{
			BoneIndex = InputWeightStream[BoneIndexOffset + 1] << 8 | BoneIndex;
			//@todo-lh: Workaround to fix issue in SPIRVEmitter of DXC; this block must be inside the if branch
			float BoneWeight = float(InputWeightStream[WeightsOffset + InfluenceIdx]) / 255.0f;
			BoneMatrix += BoneWeight * GetBoneMatrix(BoneIndex);
		}
		else
		{
			//@todo-lh: Workaround to fix issue in SPIRVEmitter of DXC; this block must be inside the if branch
			float BoneWeight = float(InputWeightStream[WeightsOffset + InfluenceIdx]) / 255.0f;
			BoneMatrix += BoneWeight * GetBoneMatrix(BoneIndex);
		}
	}
#else // GPUSKIN_UNLIMITED_BONE_INFLUENCE
	FBoneMatrix BoneMatrix = Input.BlendWeights.x * GetBoneMatrix(Input.BlendIndices.x);
	BoneMatrix += Input.BlendWeights.y * GetBoneMatrix(Input.BlendIndices.y);

#if !GPUSKIN_LIMIT_2BONE_INFLUENCES
	BoneMatrix += Input.BlendWeights.z * GetBoneMatrix(Input.BlendIndices.z);
	BoneMatrix += Input.BlendWeights.w * GetBoneMatrix(Input.BlendIndices.w);
	if (NumBoneInfluencesParam > MAX_INFLUENCES_PER_STREAM)
	{
		BoneMatrix += Input.BlendWeightsExtra.x * GetBoneMatrix(Input.BlendIndicesExtra.x);
		BoneMatrix += Input.BlendWeightsExtra.y * GetBoneMatrix(Input.BlendIndicesExtra.y);
		BoneMatrix += Input.BlendWeightsExtra.z * GetBoneMatrix(Input.BlendIndicesExtra.z);
		BoneMatrix += Input.BlendWeightsExtra.w * GetBoneMatrix(Input.BlendIndicesExtra.w);
	}
#endif//GPUSKIN_LIMIT_2BONE_INFLUENCES
#endif // GPUSKIN_UNLIMITED_BONE_INFLUENCE
	return BoneMatrix;
}

FBoneMatrix GetPreviousBoneMatrix(int Index)
{
#if GPUSKIN_USE_BONES_SRV_BUFFER
	float4 A = PreviousBoneMatrices[Index * 3 + 0];
	float4 B = PreviousBoneMatrices[Index * 3 + 1];
	float4 C = PreviousBoneMatrices[Index * 3 + 2];
	return FBoneMatrix(A,B,C);
#else
	return Bones.BoneMatrices[Index];
#endif
}

FBoneMatrix CalcPreviousBoneMatrix( FVertexFactoryInput Input )
{
#if GPUSKIN_UNLIMITED_BONE_INFLUENCE
	int NumBoneInfluences = Input.BlendOffsetCount & 0xff;
	int StreamOffset = Input.BlendOffsetCount >> 8;
	int WeightsOffset = StreamOffset + (InputWeightIndexSize * NumBoneInfluences);

	FBoneMatrix BoneMatrix = FBoneMatrix(float4(0,0,0,0), float4(0,0,0,0), float4(0,0,0,0));
	for (int InfluenceIdx = 0; InfluenceIdx < NumBoneInfluences; InfluenceIdx++)
	{
		int BoneIndexOffset = StreamOffset + (InputWeightIndexSize * InfluenceIdx);
		int BoneIndex = InputWeightStream[BoneIndexOffset];
		if (InputWeightIndexSize > 1)
		{
			BoneIndex = InputWeightStream[BoneIndexOffset + 1] << 8 | BoneIndex;
			//@todo-lh: Workaround to fix issue in SPIRVEmitter of DXC; this block must be inside the if branch
			float BoneWeight = float(InputWeightStream[WeightsOffset + InfluenceIdx]) / 255.0f;
			BoneMatrix += BoneWeight * GetPreviousBoneMatrix(BoneIndex);
		}
		else
		{
			//@todo-lh: Workaround to fix issue in SPIRVEmitter of DXC; this block must be inside the if branch
			float BoneWeight = float(InputWeightStream[WeightsOffset + InfluenceIdx]) / 255.0f;
			BoneMatrix += BoneWeight * GetPreviousBoneMatrix(BoneIndex);
		}
	}
#else // GPUSKIN_UNLIMITED_BONE_INFLUENCE
	FBoneMatrix BoneMatrix = Input.BlendWeights.x * GetPreviousBoneMatrix(Input.BlendIndices.x);
	BoneMatrix += Input.BlendWeights.y * GetPreviousBoneMatrix(Input.BlendIndices.y);

#if !GPUSKIN_LIMIT_2BONE_INFLUENCES
	BoneMatrix += Input.BlendWeights.z * GetPreviousBoneMatrix(Input.BlendIndices.z);
	BoneMatrix += Input.BlendWeights.w * GetPreviousBoneMatrix(Input.BlendIndices.w);
	if (NumBoneInfluencesParam > MAX_INFLUENCES_PER_STREAM)
	{
		BoneMatrix += Input.BlendWeightsExtra.x * GetPreviousBoneMatrix(Input.BlendIndicesExtra.x);
		BoneMatrix += Input.BlendWeightsExtra.y * GetPreviousBoneMatrix(Input.BlendIndicesExtra.y);
		BoneMatrix += Input.BlendWeightsExtra.z * GetPreviousBoneMatrix(Input.BlendIndicesExtra.z);
		BoneMatrix += Input.BlendWeightsExtra.w * GetPreviousBoneMatrix(Input.BlendIndicesExtra.w);
	}
#endif//GPUSKIN_LIMIT_2BONE_INFLUENCES
#endif // GPUSKIN_UNLIMITED_BONE_INFLUENCE
	return BoneMatrix;
}

/** transform position by weighted sum of skinning matrices */
float3 SkinPosition( FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates )
{
#if GPUSKIN_MORPH_BLEND
	float3 Position = MorphPosition(Input, Intermediates);
#else
	float3 Position = Intermediates.UnpackedPosition;
#endif

	Position += Input.PreSkinOffset;

	// Note the use of mul(Matrix,Vector), bone matrices are stored transposed
	// for tighter packing.
	Position = mul(Intermediates.BlendMatrix, float4(Position, 1));

	Position += Input.PostSkinOffset;

	return Position;
}

/** transform position by weighted sum of skinning matrices */
float3 SkinPreviousPosition( FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates )
{
#if GPUSKIN_MORPH_BLEND
	float3 Position = MorphPosition(Input, Intermediates);
#else
	float3 Position = Intermediates.UnpackedPosition;
#endif

	FBoneMatrix BlendMatrix = Intermediates.BlendMatrix;
	
	// Previous Blend Matrix (used for position in velocity rendering)

	if(PerBoneMotionBlur)
	{
		BlendMatrix = CalcPreviousBoneMatrix( Input );
	}

	Position += Input.PreSkinOffset;

	// Note the use of mul(Matrix,Vector), bone matrices are stored transposed
	// for tighter packing.
	Position = mul(BlendMatrix, float4(Position, 1));

	Position += Input.PostSkinOffset;

	return Position;
}
/** transform the tangent basis vectors */
float3x3 SkinTangents( FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates )
{
	float3x3 TangentToLocal;

	// tangent
	// -1..1
	half3 LocalTangentX = Input.TangentX;
	// -1..1 .xyz:normal, .w:contains sign of tangent basis determinant (left or right handed)
	half4 LocalTangentZ = Input.TangentZ;

#if GPUSKIN_MORPH_BLEND
	// calc new normal by offseting it with the delta
	LocalTangentZ.xyz = normalize(LocalTangentZ.xyz + Input.DeltaTangentZ);
	// derive the new tangent by orthonormalizing the new normal against
	// the base tangent vector (assuming these are normalized)
	LocalTangentX = normalize(LocalTangentX - dot(LocalTangentX, LocalTangentZ.xyz) * LocalTangentZ.xyz);
#else
	#if GPUSKIN_APEX_CLOTH

	float3x3 ClothTangent = float3x3(1,0,0, 0,1,0, 0,0,1);
	float TempClothBlendWeight = 0.0f;

	if( IsSimulatedVertex(Intermediates.ClothVertexInfluences[0]) )
	{ 
		TempClothBlendWeight = ClothBlendWeight;
		bool bPrevious = false;

        float3 NormalPosition = float3(0,0,0);
        float3 TangentPosition = float3(0,0,0);
        int NumInfluences = 0;
	    float SumWeights = 0.0;

        for (int i = 0; i < NUM_INFLUENCES_PER_VERTEX; ++i )
        {
            const FClothVertex Influence = Intermediates.ClothVertexInfluences[i];

	        if( Influence.SimulIndices.w < 0xFFFF )
	        { 
                ++NumInfluences;
		        TempClothBlendWeight = ClothBlendWeight;

                float3 A = GetClothSimulPosition(Influence.SimulIndices.x, bPrevious);
		        float3 B = GetClothSimulPosition(Influence.SimulIndices.y, bPrevious);
		        float3 C = GetClothSimulPosition(Influence.SimulIndices.z, bPrevious);

		        float3 NA = GetClothSimulNormal(Influence.SimulIndices.x, bPrevious);
		        float3 NB = GetClothSimulNormal(Influence.SimulIndices.y, bPrevious);
		        float3 NC = GetClothSimulNormal(Influence.SimulIndices.z, bPrevious);

			    float Weight = 1.0f;
			    if ( NUM_INFLUENCES_PER_VERTEX > 1 )
			    {
				    // Weight is packed in the last coordinate
				    Weight = Influence.Weight;
				    SumWeights += Weight;
			    }
			    else
			    {
				    // Single influence, weight is 1.0
	                Weight = 1.0f;
                    SumWeights = 1.0f;	   
			    }

			    NormalPosition += Weight * (Influence.BaryCoordNormal.x * (A + NA * Influence.BaryCoordNormal.w)
								          + Influence.BaryCoordNormal.y * (B + NB * Influence.BaryCoordNormal.w)
								          + Influence.BaryCoordNormal.z * (C + NC * Influence.BaryCoordNormal.w));

		        TangentPosition += Weight * (Influence.BaryCoordTangent.x * (A+NA*Influence.BaryCoordTangent.w)
								           + Influence.BaryCoordTangent.y * (B+NB*Influence.BaryCoordTangent.w)
								           + Influence.BaryCoordTangent.z * (C + NC * Influence.BaryCoordTangent.w));
	        }
        }

        if ( NumInfluences > 0 && SumWeights > 1e-4f )
        {
		    float InvWeight = 1.0f / SumWeights; 
            TangentPosition *= InvWeight;
            NormalPosition *= InvWeight;

		    TangentToLocal[0] = normalize(TangentPosition - Intermediates.SimulatedPosition);
		    TangentToLocal[2] = normalize(NormalPosition - Intermediates.SimulatedPosition);

		    // cloth data are all in world space so need to change into local space
		    TangentToLocal[0] = mul(TangentToLocal[0], (half3x3)Primitive.WorldToLocal);
		    TangentToLocal[2] = mul(TangentToLocal[2], (half3x3)Primitive.WorldToLocal);
        }
	    else
	    {
		    Intermediates.SimulatedPosition = float3(0, 0, 0);
		    TempClothBlendWeight = 0.0f;
	    }

		// derive the new binormal by getting the cross product of the normal and tangent
		// and flip vector based on sign of tangent basis determinant
		TangentToLocal[1] = cross(TangentToLocal[2], TangentToLocal[0]) * LocalTangentZ.w;
	
		ClothTangent = TangentToLocal;
	}
	#endif // GPUSKIN_APEX_CLOTH

#endif // GPUSKIN_MORPH_BLEND

	// Note the use of mul(Matrix,Vector), bone matrices are stored transposed
	// for tighter packing.
	TangentToLocal[0] = mul( Intermediates.BlendMatrix, float4(LocalTangentX, 0) );
	TangentToLocal[2] = mul( Intermediates.BlendMatrix, float4(LocalTangentZ.xyz, 0) );

	// derive the new binormal by getting the cross product of the normal and tangent
	// and flip vector based on sign of tangent basis determinant
	TangentToLocal[1] = cross(TangentToLocal[2], TangentToLocal[0]) * LocalTangentZ.w;

#if GPUSKIN_APEX_CLOTH
	return ClothTangent * TempClothBlendWeight + TangentToLocal * (1.0f - TempClothBlendWeight);
#else
	return TangentToLocal;
#endif	// GPUSKIN_APEX_CLOTH
}

FVertexFactoryIntermediates GetVertexFactoryIntermediates(FVertexFactoryInput Input)
{
	FVertexFactoryIntermediates Intermediates;
	
	Intermediates.UnpackedPosition = UnpackedPosition(Input);
#if GPUSKIN_APEX_CLOTH
	SetupClothVertex(Input.ClothVertexID, Intermediates.ClothVertexInfluences);
	if( IsSimulatedVertex(Intermediates.ClothVertexInfluences[0]) )
	{
		Intermediates.PreviousSimulatedPosition = ClothingPosition(Intermediates.ClothVertexInfluences, true);
		Intermediates.SimulatedPosition = ClothingPosition(Intermediates.ClothVertexInfluences, false);
	}
	else
	{
		Intermediates.PreviousSimulatedPosition = Intermediates.SimulatedPosition = Intermediates.UnpackedPosition;
	}
	
#endif

	Intermediates.BlendMatrix = CalcBoneMatrix( Input );

	// Fill TangentToLocal
	Intermediates.TangentToLocal = SkinTangents(Input, Intermediates);

	// Swizzle vertex color.
	Intermediates.Color = Input.Color FCOLOR_COMPONENT_SWIZZLE;

	return Intermediates;
}

float4 CalcWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
#if GPUSKIN_APEX_CLOTH
	if (IsSimulatedVertex(Intermediates.ClothVertexInfluences[0]))
	{
		// skinned positions
		float4 SkinnedPosition = float4(SkinPosition(Input, Intermediates), 1);
		float4 TransformedSkinPos = mul(SkinnedPosition, Primitive.LocalToWorld);

		// Intermediates.SimulatedPosition is a clothing position in world coord
		float3 BlendedPos = lerp(TransformedSkinPos.xyz, Intermediates.SimulatedPosition, ClothBlendWeight);
		return float4(BlendedPos + ResolvedView.PreViewTranslation, 1);
	}
	else
#endif
	{
		return TransformLocalToTranslatedWorld(SkinPosition(Input, Intermediates));
	}
}

/**
* Get the 3x3 tangent basis vectors for this vertex factory
*
* @param Input - vertex input stream structure
* @return 3x3 matrix
*/
float3x3 VertexFactoryGetTangentToLocal( FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	return Intermediates.TangentToLocal;
}

float4 VertexFactoryGetWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	return CalcWorldPosition(Input, Intermediates);
}

float4 VertexFactoryGetRasterizedWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float4 InWorldPosition)
{
	return InWorldPosition;
}

float3 VertexFactoryGetPositionForVertexLighting(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 TranslatedWorldPosition)
{
	return TranslatedWorldPosition;
}

void CalcTangentToWorld(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, out float3 TangentToWorld0, out float4 TangentToWorld2)
{
	float3x3 LocalToWorld = GetLocalToWorld3x3();

	// Remove scaling.
	half3 InvScale = Primitive.InvNonUniformScaleAndDeterminantSign.xyz;
	LocalToWorld[0] *= InvScale.x;
	LocalToWorld[1] *= InvScale.y;
	LocalToWorld[2] *= InvScale.z;

	float3x3 TangentToWorld = mul(Intermediates.TangentToLocal, LocalToWorld);

	TangentToWorld0 = TangentToWorld[0];
	TangentToWorld2 = float4(TangentToWorld[2], Input.TangentZ.w * Primitive.InvNonUniformScaleAndDeterminantSign.w);
}

float3 VertexFactoryGetWorldNormal(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	float3 TangentToWorld0; 
	float4 TangentToWorld2;
	CalcTangentToWorld(Input, Intermediates, TangentToWorld0, TangentToWorld2);

	return TangentToWorld2.xyz;
}

FVertexFactoryInterpolantsVSToPS VertexFactoryGetInterpolantsVSToPS(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, FMaterialVertexParameters VertexParameters)
{
	FVertexFactoryInterpolantsVSToPS Interpolants;

#if NUM_TEX_COORD_INTERPOLATORS
	float2 CustomizedUVs[NUM_TEX_COORD_INTERPOLATORS];
	GetMaterialCustomizedUVs(VertexParameters, CustomizedUVs);
	GetCustomInterpolators(VertexParameters, CustomizedUVs);

	UNROLL
	for (int CoordinateIndex = 0; CoordinateIndex < NUM_TEX_COORD_INTERPOLATORS; CoordinateIndex++)
	{
		Interpolants.TexCoords[CoordinateIndex] = CustomizedUVs[CoordinateIndex];
	}
#endif

	Interpolants.TangentToWorld0.w = 0;
	CalcTangentToWorld(Input, Intermediates, Interpolants.TangentToWorld0.xyz, Interpolants.TangentToWorld2);
#if USE_WORLDVERTEXNORMAL_CENTER_INTERPOLATION
	Interpolants.TangentToWorld2_Center = Interpolants.TangentToWorld2;
#endif

#if INTERPOLATE_VERTEX_COLOR
	Interpolants.Color = Intermediates.Color;
#endif

#if INSTANCED_STEREO
	Interpolants.EyeIndex = 0;
#endif

	return Interpolants;
}

// @return previous translated world position
float4 VertexFactoryGetPreviousWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	float4x4 PreviousLocalToWorldTranslated = Primitive.PreviousLocalToWorld;

	PreviousLocalToWorldTranslated[3][0] += ResolvedView.PrevPreViewTranslation.x;
	PreviousLocalToWorldTranslated[3][1] += ResolvedView.PrevPreViewTranslation.y;
	PreviousLocalToWorldTranslated[3][2] += ResolvedView.PrevPreViewTranslation.z;

	float4 PrevSkinPosInWorld = mul(float4(SkinPreviousPosition(Input, Intermediates),1), PreviousLocalToWorldTranslated);
#if GPUSKIN_APEX_CLOTH
	if (IsSimulatedVertex(Intermediates.ClothVertexInfluences[0]))
	{
		float3 PrevSimulatedPos = Intermediates.PreviousSimulatedPosition + ResolvedView.PrevPreViewTranslation;
		float3 BlendedPrevPos = lerp(PrevSkinPosInWorld.xyz, PrevSimulatedPos.xyz, ClothBlendWeight);
		return float4(BlendedPrevPos, 1);
	}
#endif // GPUSKIN_APEX_CLOTH

	return PrevSkinPosInWorld;
}


#if USING_TESSELLATION
float2 VertexFactoryGetTextureCoordinateDS( FVertexFactoryInterpolantsVSToDS Interpolants )
{
#if NUM_MATERIAL_TEXCOORDS
	return Interpolants.InterpolantsVSToPS.TexCoords[0].xy;
#else // #if NUM_MATERIAL_TEXCOORDS
	return float2(0,0);
#endif // #if NUM_MATERIAL_TEXCOORDS
}

FVertexFactoryInterpolantsVSToPS VertexFactoryAssignInterpolants(FVertexFactoryInterpolantsVSToDS Input)
{
	return Input.InterpolantsVSToPS;
}

FVertexFactoryInterpolantsVSToDS VertexFactoryGetInterpolantsVSToDS(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, FMaterialVertexParameters VertexParameters)
{
	FVertexFactoryInterpolantsVSToDS Interpolants;
	
	Interpolants.InterpolantsVSToPS = VertexFactoryGetInterpolantsVSToPS(Input, Intermediates, VertexParameters);
	
	return Interpolants;
}

/** Converts from vertex factory specific interpolants to a FMaterialTessellationParameters, which is used by material inputs. */
FMaterialTessellationParameters GetMaterialTessellationParameters(FVertexFactoryInterpolantsVSToDS Interpolants, float3 CameraLocalWorldPosition)
{
	FMaterialTessellationParameters	Result;
#if NUM_TEX_COORD_INTERPOLATORS
	UNROLL
	for (int CoordinateIndex = 0;CoordinateIndex < NUM_TEX_COORD_INTERPOLATORS;CoordinateIndex++)
	{
		Result.TexCoords[CoordinateIndex] = Interpolants.InterpolantsVSToPS.TexCoords[CoordinateIndex];
	}
#endif

	half3 TangentToWorld0 = Interpolants.InterpolantsVSToPS.TangentToWorld0.xyz;
	half4 TangentToWorld2 = Interpolants.InterpolantsVSToPS.TangentToWorld2;

#if INTERPOLATE_VERTEX_COLOR
	Result.VertexColor = Interpolants.InterpolantsVSToPS.Color;
#endif

	Result.TangentToWorld = AssembleTangentToWorld( TangentToWorld0, TangentToWorld2 );

	Result.TangentToWorldPreScale = 1;


	Result.WorldPosition = CameraLocalWorldPosition + ResolvedView.WorldCameraOrigin;

	return Result;
}

FVertexFactoryInterpolantsVSToDS VertexFactoryInterpolate(FVertexFactoryInterpolantsVSToDS a, float aInterp, FVertexFactoryInterpolantsVSToDS b, float bInterp)
{
	// Default initialize. Otherwise, some graphics pipelines that
	// couple tessellation with geometry shaders won't write to all TEXCOORD semantics,
	// but read from them when <FVertexFactoryInterpolantsVSToDS> is being copied as a whole.
	FVertexFactoryInterpolantsVSToDS O = (FVertexFactoryInterpolantsVSToDS)0;

	TESSELLATION_INTERPOLATE_MEMBER(InterpolantsVSToPS.TangentToWorld0.xyz);
	TESSELLATION_INTERPOLATE_MEMBER(InterpolantsVSToPS.TangentToWorld2);
#if INTERPOLATE_VERTEX_COLOR
	TESSELLATION_INTERPOLATE_MEMBER(InterpolantsVSToPS.Color);
#endif

#if NUM_TEX_COORD_INTERPOLATORS
	UNROLL
	for(int tc = 0; tc < NUM_TEX_COORD_INTERPOLATORS; ++tc)
	{
		TESSELLATION_INTERPOLATE_MEMBER(InterpolantsVSToPS.TexCoords[tc]);
	}
#endif

	return O;
}

float3x3 VertexFactoryGetTangentToLocalDS(FVertexFactoryInterpolantsVSToDS Interpolants)
{
	// This duplicates stuff already going on in GetMaterialTessellationParameters(), so
	// maybe the hull shader could leverage that instead?

	half3 TangentToWorld0 = Interpolants.InterpolantsVSToPS.TangentToWorld0.xyz;
	half4 TangentToWorld2 = Interpolants.InterpolantsVSToPS.TangentToWorld2;

	float3x3 TangentToWorld = AssembleTangentToWorld( TangentToWorld0, TangentToWorld2 );
	
	return TangentToWorld;
}

#endif // #if USING_TESSELLATION

float4 VertexFactoryGetTranslatedPrimitiveVolumeBounds(FVertexFactoryInterpolantsVSToPS Interpolants)
{
	return 0;
}

uint VertexFactoryGetPrimitiveId(FVertexFactoryInterpolantsVSToPS Interpolants)
{
	return 0;
}
