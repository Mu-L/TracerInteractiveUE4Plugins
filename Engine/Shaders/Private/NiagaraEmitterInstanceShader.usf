// Copyright 1998-2018 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
NiagaraSimulationShader.usf: 
=============================================================================*/

#if GPU_SIMULATION
	#include "/Engine/Public/Platform.ush"
#endif

#include "Definitions.usf"


//TODO: Add common utility code for things such as reading data sources, reading from and writing to events.

#if GPU_SIMULATION
	/** Returns clip space W, which is world space distance along the View Z axis. Note if you need DeviceZ LookupDeviceZ() is the faster option */
	float CalcSceneDepth(float2 ScreenUV)
	{
	#if SCENE_TEXTURES_DISABLED
		return 0.0f;
	#else
		#if FEATURE_LEVEL > FEATURE_LEVEL_ES3_1 || MOBILE_FORCE_DEPTH_TEXTURE_READS
			return ConvertFromDeviceZ(Texture2DSampleLevel(SceneDepthTexture, SceneDepthTextureSampler, ScreenUV, 0).r);
		#else
			#if COMPILER_GLSL_ES2
				#if IOS
					// Only call FramebufferFetch when actually compiling for IOS ES2.
					return FramebufferFetchES2().w;
				#elif WEBGL
					return Texture2DSampleLevel(SceneAlphaCopyTexture, SceneAlphaCopyTextureSampler, ScreenUV, 0).r;
				#else 
					float SceneW = ConvertFromDeviceZ(Texture2DSampleLevel(SceneDepthTexture, SceneDepthTextureSampler, ScreenUV, 0).r);		
					return DepthbufferFetchES2(SceneW, View.InvDeviceZToWorldZTransform[2], View.InvDeviceZToWorldZTransform[3]);
				#endif 
			#elif METAL_PROFILE && !MAC
					return FramebufferFetchES2().w;
			#else
				return ConvertFromDeviceZ(Texture2DSampleLevel(SceneDepthTexture, SceneDepthTextureSampler, ScreenUV, 0).r);
			#endif
		#endif
	#endif
	}


	static uint3 GDispatchThreadId;
	uint EmitterTickCounter;

	#define THREADGROUP_SIZE 64

	// 4D random number generator inspired by PCGs (permuted congruential generator)
	// COPY FROM RANDOM.USH, which we can't easily include here due to a sizable dependency chain
	uint4 NiagaraRand4DPCG32(int4 p)
	{
		// taking a signed int then reinterpreting as unsigned gives good behavior for negatives
		uint4 v = uint4(p);

		// Linear congruential step.
		v = v * 1664525u + 1013904223u;

		// swapping low and high bits makes all 32 bits pretty good
		v = v * (1u << 16u) + (v >> 16u);

		// final shuffle
		v.x += v.y*v.w;
		v.y += v.z*v.x;
		v.z += v.x*v.y;
		v.w += v.y*v.z;
		v.x += v.y*v.w;
		v.y += v.z*v.x;
		v.z += v.x*v.y;
		v.w += v.y*v.z;

		return v;
	}



	// using Marc's PCG random implementation, sequentially; initially seed with DispatchThreadID and tick counter,
	// then repeat seeding with the result from the PRNG; should be deterministic
	// TODO: use this in the VM for CPU sims as well
	//
	static uint4 RandomVal = 2308975;
	float4 rand4()
	{
		RandomVal = NiagaraRand4DPCG32( int4(GDispatchThreadId+(RandomVal/573).xyz, RandomVal.x/921) + int4(EmitterTickCounter.xxxx));
		return float4(RandomVal.xyzw) / float4(0xFFFFFFFF,0xFFFFFFFF,0xFFFFFFFF,0xFFFFFFFF);
	}

	float rand(float x)
	{
		return rand4().x * x;
	}

	float2 rand(float2 x)
	{
		return float2(rand4().x, rand4().y) * x;
	}

	float3 rand(float3 x)
	{
		return float3(rand4().x, rand4().y, rand4().z) * x;
	}

	float4 rand(float4 x) 
	{
		return rand4() * x;
	}

	int rand(int x)
	{
		RandomVal = NiagaraRand4DPCG32(int4(GDispatchThreadId + (RandomVal / 57843).xyz, 54121) + int4(EmitterTickCounter.xxxx));
		return RandomVal.x % x+1;
	}

#else

	//Temporary random hacks
	float2 rand(float2 x)
	{
		return float2(rand(x.x), rand(x.y));
	}

	float3 rand(float3 x)
	{
		return float3(rand(x.x), rand(x.y), rand(x.z));
	}

	float4 rand(float4 x) 
	{
		return float4(rand(x.x), rand(x.y), rand(x.z), rand(x.w));
	}

	int rand(int x);
#endif


/** Hooks into the stats system. */
void EnterStatScope(int ScopeIdx);
void ExitStatScope();

#if VM_SIMULATION
	float noise(float x);
	float noise(float2 x);
	float noise(float3 x);

	//Total hack to get around the cross compiler converting fmod() to "X - (Y * trunc(X/Y))";
	//On gpu just define these as fmod(x,y)
	float4 Modulo(float4 x, float4 y);
	float3 Modulo(float3 x, float3 y);
	float2 Modulo(float2 x, float2 y);
	float Modulo(float x, float y);

	/** Returns the index for this particle in the current execution context. On gpu this'll likely be derived from DispatchThreadId */
	int ExecIndex();

	//Some functions that we use to map to special VM operations for reading in data.
	//TODO: replace with proper buffer reading capability and use standard hlsl.
	int AcquireIndex(int DataSetID, bool DoAcquire);

	float InputDataFloat(int DataSetIndex, int RegisterIdx);  //DataSetIndex is 0 for main dataset
	int InputDataInt(int DataSetIndex, int RegisterIdx);
	bool InputDataBool(int DataSetIndex, int RegisterIdx);

	float InputDataNoadvanceFloat(int DataSetIndex, int RegisterIdx);  //DataSetIndex is 0 for main dataset
	int InputDataNoadvanceInt(int DataSetIndex, int RegisterIdx);
	bool InputDataNoadvanceBool(int DataSetIndex, int RegisterIdx);

	void OutputDataFloat(int DataSetIndex, int RegisterIndex, int InstanceIndex, float Value);
	void OutputDataInt(int DataSetIndex, int RegisterIndex, int InstanceIndex, int Value);
	void OutputDataBool(int DataSetIndex, int RegisterIndex, int InstanceIndex, bool Value);
#endif

#if GPU_SIMULATION
	int ExecIndex()
	{
		return GDispatchThreadId.x;
	}

	uint StartInstancePhase0;
	uint StartInstancePhase1;
	uint TotalNumInstances;

	uint ComponentBufferSizeRead;
	uint ComponentBufferSizeWrite;
	int	NumEventsPerParticle;
	int NumParticlesPerEvent;
	uint CopyInstancesBeforeStart;

	Buffer<float> InputFloat;
	Buffer<int> InputInt;
	RWBuffer<int> RWOutputInt;
	RWBuffer<float> RWOutputFloat;
	RWBuffer<int> RWDataSetIndices;

	int AcquireIndex(int DataSetID, bool bDoAcquire)
	{
		int PrevIdx = -1;
		if(bDoAcquire == true)
		{
			InterlockedAdd(RWDataSetIndices[DataSetID*5+1], 1, PrevIdx);
		}
		return PrevIdx;
	}

	float InputDataFloat(int DataSetIndex, int RegisterIdx, int InstanceIdx)
	{
		return InputFloat[RegisterIdx*ComponentBufferSizeRead + InstanceIdx];
	}

	float InputDataInt(int DataSetIndex, int RegisterIdx, int InstanceIdx)
	{
		return InputInt[RegisterIdx*ComponentBufferSizeRead + InstanceIdx];
	}

	void OutputDataFloat(int DataSetIndex, int RegisterIndex, int InstanceIndex, float Value)
	{
		RWOutputFloat[RegisterIndex*ComponentBufferSizeWrite + InstanceIndex + StartInstancePhase0] = Value;
	}

	void OutputDataInt(int DataSetIndex, int RegisterIndex, int InstanceIndex, int Value)
	{
		RWOutputInt[RegisterIndex*ComponentBufferSizeWrite + InstanceIndex + StartInstancePhase0] = Value;
	}

	void EnterStatScope(int ID)	{}
	void ExitStatScope()	{}
#endif

//Include the simulation shader code generated by the node graph.
#include "/Engine/Generated/NiagaraEmitterInstance.usf"


//The VectorVM shader needs only the generated code but a compute shader requires some wrapping.
#if GPU_SIMULATION

[numthreads(THREADGROUP_SIZE, 1, 1)]
void SimulateMainComputeCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	GDispatchThreadId = DispatchThreadId;

	/*
	if(CopyInstancesBeforeStart == 1)
	{
		StartInstancePhase0 = 0;
	}
	*/

	uint InstanceID = StartInstancePhase0 + DispatchThreadId.x;

	if(InstanceID < StartInstancePhase0+TotalNumInstances )
	{
		// calculate event index for the current instance; this is primarily for spawn events, where we do a single read from a secondary DataSet for a subset of particles
		//  for all-particle events, this will always end up being 0 because NumParticlesPerEvent = total particles in the set
		uint EventIndex = 0; //(InstanceID-SimulateStartInstance) / NumParticlesPerEvent;

		// before the simulation start - either update or copy
		//
		if(InstanceID < StartInstancePhase1)
		{
			// copy instances before simulation start inst (e.g. for event spawns)
			/*
			if(CopyInstancesBeforeStart)
			{
				CopyInstance(InstanceID);
			}
			else
			*/
			{
				SimulateMain(InstanceID, EventIndex, 1);
			}

		}

		// after simulation start - spawn phase
		//
		else if(InstanceID >= StartInstancePhase1)
		{
				SimulateMain(InstanceID, EventIndex, 0);		// SimulateMain spawn phase
		}
	}

	// fill in the rest of RWDataSetIndices for the DrawIndirect call for the main data set; number of instances is incremented in AcquireIndex
	if(InstanceID == 0)
	{
		RWDataSetIndices[0] = 6;		// IndexCountPerInstance; TODO: need to pass this in from outside based on renderer setup
		RWDataSetIndices[2] = 0;		// StartIndexLocation
		RWDataSetIndices[3] = 0;		// BaseVertexLocation
		RWDataSetIndices[4] = 0;		// StartInstanceLocation
	}
}

#endif//GPU_SIMULATION
