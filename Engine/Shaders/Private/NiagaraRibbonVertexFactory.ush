// Copyright 1998-2018 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	ParticleBeamTrailVertexFactory.hlsl: Particle vertex factory shader code.
=============================================================================*/

#include "VertexFactoryCommon.ush"




float3 TransformPosition(float3 InPosition)
{
	return mul(float4(InPosition,1.0), NiagaraSpriteVF.LocalToWorld).xyz;
}

float3 TransformVector(float3 InVector)
{
	return mul(float4(InVector,0.0), NiagaraSpriteVF.LocalToWorldInverseTransposed).xyz;
}


/*********************************************************************************
 *							Particle specific
 *********************************************************************************/
/*
float3 GetNiagaraParticlePosition(uint InstanceID)
{
	return TransformPosition(GetVec3(NiagaraRibbonVF.PositionDataOffset, InstanceID));
}
float GetNiagaraParticleRibbonWidth(uint InstanceID)
{
	return GetFloat(NiagaraRibbonVF.WidthDataOffset, InstanceID);
}
float GetNiagaraParticleRibbonTwist(uint InstanceID)
{
	return GetFloat(NiagaraRibbonVF.TwistDataOffset, InstanceID);
}
float4 GetNiagaraParticleColor(uint InstanceID)
{
	return GetVec4(NiagaraRibbonVF.ColorDataOffset, InstanceID);
}
#if USE_DYNAMIC_PARAMETERS
float4 GetNiagaraParticleDynamicParameters(uint InstanceID)
{
	if (NiagaraRibbonVF.MaterialParamDataOffset == -1)
	{
		return float4(1.0f, 1.0f, 1.0f, 1.0f);
	}
	return GetVec4(NiagaraRibbonVF.MaterialParamDataOffset, InstanceID);
}
#endif


float3 GetNiagaraParticleFacingVector(uint InstanceID)
{
	float3 Val = float3(0.0f, 0.0f, 0.0f);
    if(NiagaraRibbonVF.FacingOffset != -1)
	{
		Val = GetVec3(NiagaraRibbonVF.FacingOffset, InstanceID);
	}
	return TransformVector(Val);
}
*/




struct FVertexFactoryInput
{
	float3	Position				: ATTRIBUTE0;
	float3 Direction				: ATTRIBUTE1;
	float	Size					: ATTRIBUTE2;
	float4	Color					: ATTRIBUTE3;
	float4	TexCoord				: ATTRIBUTE4;

	float Twist						: ATTRIBUTE5;
	float3 CustomFacingVector		: ATTRIBUTE6;

#if USE_DYNAMIC_PARAMETERS
	float4 DynamicParameter0		: ATTRIBUTE7;
#endif	//USE_DYNAMIC_PARAMETERS

/** Optional instance ID for vertex layered rendering */
#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
	uint VertexID	: SV_VertexID;
#endif

/** Optional instance ID for vertex layered rendering */
#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4 && ONEPASS_POINTLIGHT_SHADOW && USING_VERTEX_SHADER_LAYER
	uint InstanceId	: SV_InstanceID;
#endif
};

struct FVertexFactoryInterpolantsVSToPS
{
	// First row of the tangent to world matrix
	float3	TangentToWorld0	: TANGENTTOWORLD0;
	// Last row of the tangent to world matrix in xyz
	float4	TangentToWorld2	: TANGENTTOWORLD2;

	float4	TexCoord			: TEXCOORD2;

#if NEEDS_PARTICLE_COLOR
	float4  Color				: TEXCOORD3;
#endif

#if USE_DYNAMIC_PARAMETERS
	float4 DynamicParameter0	: TEXCOORD4;
#endif	//USE_DYNAMIC_PARAMETERS

#if INSTANCED_STEREO
	nointerpolation uint PackedEyeIndex : PACKED_EYE_INDEX;
#endif
};

struct FVertexFactoryIntermediates
{
	/** The color of the sprite. */
	float4 Color;
	float RibbonWidth;
	float4 Right, Up;
	float3 CameraDirection;
};

/** Converts from vertex factory specific interpolants to a FMaterialPixelParameters, which is used by material inputs. */
FMaterialPixelParameters GetMaterialPixelParameters(FVertexFactoryInterpolantsVSToPS Interpolants, float4 SvPosition)
{
	// GetMaterialPixelParameters is responsible for fully initializing the result
	FMaterialPixelParameters Result = MakeInitializedMaterialPixelParameters();

#if NUM_MATERIAL_TEXCOORDS
	#if NUM_MATERIAL_TEXCOORDS >= 1
		Result.TexCoords[0] = Interpolants.TexCoord.xy;
	#if NUM_MATERIAL_TEXCOORDS >= 2
		Result.TexCoords[1] = Interpolants.TexCoord.zw;
	#endif	// >= 2
	#endif	// >= 1
	#if NUM_MATERIAL_TEXCOORDS > 2
		UNROLL
		for(int CoordinateIndex = 2;CoordinateIndex < NUM_MATERIAL_TEXCOORDS;CoordinateIndex++)
		{
			Result.TexCoords[CoordinateIndex] = Interpolants.TexCoord.xy;
		}
	#endif	// > 2
#endif
	Result.VertexColor = 1;

#if NEEDS_PARTICLE_COLOR
	Result.Particle.Color = Interpolants.Color;
#endif

	half3 TangentToWorld0 = Interpolants.TangentToWorld0.xyz;
	half4 TangentToWorld2 = Interpolants.TangentToWorld2;
	Result.TangentToWorld = AssembleTangentToWorld( TangentToWorld0, TangentToWorld2 );
	Result.UnMirrored = 1;

#if LIGHTMAP_UV_ACCESS
	Result.LightmapUVs = Interpolants.TexCoord.xy;
#endif

	Result.TwoSidedSign = 1;

#if USE_DYNAMIC_PARAMETERS
	Result.Particle.DynamicParameter = Interpolants.DynamicParameter0;
#endif	//USE_DYNAMIC_PARAMETERS

	return Result;
}

/** Converts from vertex factory specific input to a FMaterialVertexParameters, which is used by vertex shader material inputs. */
FMaterialVertexParameters GetMaterialVertexParameters(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 WorldPosition, float3x3 TangentToLocal)
{
	FMaterialVertexParameters Result = (FMaterialVertexParameters)0;
	Result.WorldPosition = WorldPosition;
	Result.VertexColor = Input.Color;
	Result.TangentToWorld = mul(TangentToLocal, GetLocalToWorld3x3()); 
	Result.Particle.Color = Intermediates.Color;
	Result.PreSkinnedPosition = Input.Position.xyz;
	Result.PreSkinnedNormal = TangentToLocal[2].xyz;

#if NUM_MATERIAL_TEXCOORDS_VERTEX
	for(int CoordinateIndex = 0; CoordinateIndex < NUM_MATERIAL_TEXCOORDS_VERTEX; CoordinateIndex++)
	{
		Result.TexCoords[CoordinateIndex] = Input.TexCoord.xy;
	}
#endif
	return Result;
}

void GetLightMapCoordinates(FVertexFactoryInterpolantsVSToPS Interpolants, out float2 LightmapUV0, out float2 LightmapUV1)
{
	LightmapUV0 = LightmapUV1 = 0;
}

half2 GetShadowMapCoordinate(FVertexFactoryInterpolantsVSToPS Interpolants)
{
	return 0;
}

float3 SafeNormalize(float3 V)
{
	return V / sqrt(max(dot(V,V),0.01));
}

void GetTangents(FVertexFactoryInput Input,out float4 Right,out float4 Up, out float3 CameraDirection)
{
	float4	Position			= TransformLocalToTranslatedWorld(Input.Position.xyz);
			//OldPosition			= TransformLocalToTranslatedWorld(Input.OldPosition.xyz);

	CameraDirection		= SafeNormalize(ResolvedView.WorldCameraOrigin - Input.Position.xyz );		// TODO: handle local space
	float3 ParticleDirection	= Input.Direction; //SafeNormalize(Position.xyz - OldPosition.xyz);

	float4	Right_Square		= BeamTrailVF.CameraRight,
			Up_Square			= BeamTrailVF.CameraUp;			
			
	float4	Right_Rotated		= (-1.0 * cos(Input.Twist) * Up_Square) + (sin(Input.Twist) * Right_Square),
			Up_Rotated			= (       sin(Input.Twist) * Up_Square) + (cos(Input.Twist) * Right_Square);

	float4	Right_Velocity		= float4( SafeNormalize( cross( CameraDirection, ParticleDirection	) ), 0.0 ),
			Up_Velocity			= float4( ParticleDirection, 0.0 );

	float3 NormDir = SafeNormalize(Input.Direction.xyz);
	float3 RightDir;
	if(NiagaraRibbonVF.UseCustomFacing)
	{
		RightDir = SafeNormalize(cross(NormDir, Input.CustomFacingVector));
	}
	else
	{
		//float Blend = abs(dot(NormDir, SafeNormalize(CameraDirection.xyz)));
		RightDir = SafeNormalize(cross(NormDir, CameraDirection.xyz));
	}
	
	Right = float4(RightDir, 0.0f);
	Up = float4(NormDir, 0.0f);
	return;
	/*
	//	enum EParticleScreenAlignment
	//	{
	//		PSA_Square,
	//		PSA_Rectangle,
	//		PSA_Velocity
	//	};
	Right				= BeamTrailVF.ScreenAlignment.x > 1.5f ? Right_Velocity : Right_Rotated;
	Up					= BeamTrailVF.ScreenAlignment.x > 1.5f ? Up_Velocity	: Up_Rotated;
	*/
}

float4 CalcWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	// expand and align
	int Sgn = (((int)Input.VertexID & 0x1) * 2) - 1;
	float3 NormDir = SafeNormalize(Input.Direction.xyz);
	float3 RightDir;
	if(NiagaraRibbonVF.UseCustomFacing)
	{
		RightDir = SafeNormalize(cross(NormDir, Input.CustomFacingVector));
	}
	else
	{
		//float Blend = abs(dot(NormDir, SafeNormalize(Intermediates.CameraDirection.xyz)));
		RightDir = SafeNormalize(cross(NormDir, Intermediates.CameraDirection.xyz));
	}
	float3 UpDir = cross(RightDir, NormDir);

	//float4	Right_Rotated		= (-1.0 * cos(Input.Twist) * UpDir) + (sin(Input.Twist) * RightDir),
	//		Up_Rotated			= (       sin(Input.Twist) * UpDir) + (cos(Input.Twist) * RightDir);

	float3 Pos = Input.Position.xyz + RightDir * Sgn * Input.Size.xxx;
	return TransformLocalToTranslatedWorld(Pos);
}

/** derive basis vectors */
float3x3 CalcTangentBasis(FVertexFactoryInput Input)
{
	float4	Right,
			Up;
	float3	CameraDir;
	GetTangents(Input,Right,Up, CameraDir);
	return float3x3(
			Right.xyz,
			Up.xyz,
			-normalize(cross(Right.xyz,Up.xyz))
			);
}

FVertexFactoryIntermediates GetVertexFactoryIntermediates(FVertexFactoryInput Input)
{
	FVertexFactoryIntermediates Intermediates;

	Intermediates.Color = Input.Color;
    Intermediates.RibbonWidth = Input.Size;
	GetTangents(Input, Intermediates.Right, Intermediates.Up, Intermediates.CameraDirection);

	return Intermediates;
}

float4 VertexFactoryGetWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	return CalcWorldPosition(Input, Intermediates);
}

float4 VertexFactoryGetRasterizedWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float4 InWorldPosition)
{
	return InWorldPosition;
}

float3 VertexFactoryGetPositionForVertexLighting(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 TranslatedWorldPosition)
{
	return TranslatedWorldPosition;
}

FVertexFactoryInterpolantsVSToPS VertexFactoryGetInterpolantsVSToPS(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, FMaterialVertexParameters VertexParameters)
{
	FVertexFactoryInterpolantsVSToPS Interpolants;
	Interpolants.TexCoord	= Input.TexCoord;

#if NEEDS_PARTICLE_COLOR
	Interpolants.Color		= Intermediates.Color;
#endif

#if USE_DYNAMIC_PARAMETERS
	Interpolants.DynamicParameter0 = Input.DynamicParameter0;
#endif	//USE_DYNAMIC_PARAMETERS

	float3x3 TangentToLocal = CalcTangentBasis(Input);
	float3x3 TangentToWorld = mul(TangentToLocal, GetLocalToWorld3x3());

	Interpolants.TangentToWorld0 = TangentToWorld[0];
	Interpolants.TangentToWorld2 = float4(TangentToWorld[2], sign(determinant(TangentToLocal)));

#if INSTANCED_STEREO
	Interpolants.PackedEyeIndex = 0;
#endif

	return Interpolants;
}

float4 VertexFactoryGetPreviousWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	return VertexFactoryGetWorldPosition(Input, Intermediates);
}

/**
* Get the 3x3 tangent basis vectors for this vertex factory
*
* @param Input - vertex input stream structure
* @return 3x3 matrix
*/
float3x3 VertexFactoryGetTangentToLocal( FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates )
{
	return CalcTangentBasis(Input);
}

#if INSTANCED_STEREO
uint VertexFactoryGetEyeIndex(uint InstanceId)
{
	return InstanceId & 1;
}
#endif

float4 VertexFactoryGetTranslatedPrimitiveVolumeBounds(FVertexFactoryInterpolantsVSToPS Interpolants)
{
	return 0;
}
