// Copyright 1998-2019 Epic Games, Inc. All Rights Reserved.

// Light types: should match SceneTypes.h until there is a common header
#define LIGHT_TYPE_DIRECTIONAL		0 
#define LIGHT_TYPE_POINT			1 
#define LIGHT_TYPE_SPOT				2 
#define LIGHT_TYPE_RECT				3 
#define LIGHT_TYPE_MAX				4 

float GetHitT(FMaterialClosestHitPayload HitInfo)
{
	return HitInfo.HitT;
}

bool IsHit(RayDesc Ray, FMaterialClosestHitPayload HitInfo)
{
	float t = HitInfo.HitT;
	return t > Ray.TMin && t < Ray.TMax;
}

bool IsHit(RayDesc Ray, FPackedMaterialClosestHitPayload HitInfo)
{
	float t = HitInfo.HitT;
	return t > Ray.TMin && t < Ray.TMax;
}

float GetRoughnessFade(float Roughness, float MaxRoughness)
{
	float RoughnessMaskScale = -2.0 / MaxRoughness;
	return saturate(Roughness * RoughnessMaskScale + 2.0);
}

float3 GenerateReflectedRayDirection(
	float3 IncidentDirection,
	float3 WorldNormal,
	float Roughness,
	float2 RandSample
)
{
	float3 RayDirection;
	if (Roughness < 0.001) //ReflectionSmoothClamp)
	{
		RayDirection = reflect(IncidentDirection, WorldNormal);
	}
	else
	{
		float3 N = WorldNormal;
		float3 V = -IncidentDirection;
		float2 E = RandSample;

		float3x3 TangentBasis = GetTangentBasis(N);
		float3 TangentV = mul(TangentBasis, V);

		float NoV = saturate(dot(V, WorldNormal));

		float4 Sample = ImportanceSampleVisibleGGX(E, Pow4(Roughness), TangentV);

		float3 H = mul(Sample.xyz, TangentBasis);
		float3 L = 2 * dot(V, H) * H - V;

		RayDirection = L;
	}

	return RayDirection;
}

float TraceShadowRay(
	in RayDesc Ray,
	in FRayCone ShadowRayCone)
{
	uint RayFlags = RAY_FLAG_CULL_BACK_FACING_TRIANGLES | RAY_FLAG_ACCEPT_FIRST_HIT_AND_END_SEARCH | RAY_FLAG_SKIP_CLOSEST_HIT_SHADER;

	FMaterialClosestHitPayload Payload = TraceRayInternal(
		TLAS,      // AccelerationStructure
		RayFlags,
		RAY_TRACING_MASK_SHADOW,
		RAY_TRACING_SHADER_SLOT_SHADOW, // RayContributionToHitGroupIndex
		RAY_TRACING_NUM_SHADER_SLOTS,   // MultiplierForGeometryContributionToShaderIndex
		0,         // MissShaderIndex
		Ray,       // RayDesc
		ShadowRayCone
	);

	return Payload.HitT;
}

float3 SampleAreaLightDirection(
	in FDeferredLightData LightData, 
	in FMaterialClosestHitPayload Payload,
	in uint LightType, 
	in RandomSequence RandSequence )
{
	float3 ShadowRayDirection;
	float3 WorldPosition = Payload.WorldPos;

	float3 RayOrigin;
	uint RayTMin, RayTMax;
	float RayPdf;

	uint DummyVariable;
	float2 RandSample = RandomSequence_GenerateSample2D(RandSequence, DummyVariable);

	FLightShaderParameters LightParameters;
	LightParameters.Position = LightData.Position;
	LightParameters.SpotAngles = LightData.SpotAngles;
	LightParameters.SourceRadius = LightData.SourceRadius;
	LightParameters.SourceLength = LightData.SourceLength;
	LightParameters.Tangent = LightData.Tangent;
	LightParameters.Direction = LightData.Direction;

	if (LightType == LIGHT_TYPE_DIRECTIONAL)
	{
		GenerateDirectionalLightOcclusionRay(
			LightParameters,
			WorldPosition, Payload.WorldNormal,
			RandSample,
			/* out */ RayOrigin,
			/* out */ ShadowRayDirection,
			/* out */ RayTMin,
			/* out */ RayTMax);
	}
	else if (LightType == LIGHT_TYPE_SPOT)
	{
		GenerateSpotLightOcclusionRay(
			LightParameters,
			WorldPosition, Payload.WorldNormal,
			RandSample,
			/* out */ RayOrigin,
			/* out */ ShadowRayDirection,
			/* out */ RayTMin,
			/* out */ RayTMax);
	}
	else if (LightType == LIGHT_TYPE_POINT)
	{
		if (LightData.SourceRadius == 0)
		{
			GeneratePointLightOcclusionRay(
				LightParameters,
				WorldPosition, Payload.WorldNormal,
				RandSample,
				/* out */ RayOrigin,
				/* out */ ShadowRayDirection,
				/* out */ RayTMin,
				/* out */ RayTMax);
		}
		else
		{
			GenerateSphereLightOcclusionRay(
				LightParameters,
				WorldPosition, Payload.WorldNormal,
				RandSample,
				/* out */ RayOrigin,
				/* out */ ShadowRayDirection,
				/* out */ RayTMin,
				/* out */ RayTMax,
				/* out */ RayPdf);
		}
	}
	else if (LightType == LIGHT_TYPE_RECT)
	{
		GenerateRectLightOcclusionRay(
			LightParameters,
			WorldPosition, Payload.WorldNormal,
			RandSample,
			/* out */ RayOrigin,
			/* out */ ShadowRayDirection,
			/* out */ RayTMin,
			/* out */ RayTMax,
			/* out */ RayPdf);
	}

	return ShadowRayDirection;
}

FPackedMaterialClosestHitPayload TraceRayInternalPacked(in RaytracingAccelerationStructure TLAS,
	in uint RayFlags,
	in uint InstanceInclusionMask,
	in uint RayContributionToHitGroupIndex,
	in uint MultiplierForGeometryContributionToShaderIndex,
	in uint MissShaderIndex,
	in RayDesc Ray,
	inout FRayCone RayCone)
{
	FPackedMaterialClosestHitPayload Payload = (FPackedMaterialClosestHitPayload)0;
	Payload.RayCone = RayCone;

	TraceRay
	(
		TLAS,
		RayFlags,
		InstanceInclusionMask,
		RayContributionToHitGroupIndex,
		MultiplierForGeometryContributionToShaderIndex,
		MissShaderIndex,
		Ray,
		Payload
	);

	RayCone = Payload.RayCone;
	return Payload;
}

uint2 CullDirectLighting(
	in RayDesc Ray,
	in FRayCone RayCone,
	in float3 WorldPosition,
	in float3 WorldNormal,
	in RandomSequence RandSequence,
	in bool bReflectedShadows,
	in float ShadowMaxNormalBias)
{

	uint2 LightCullMask = (uint2)0;

	//#dxr_todo: think on the more efficient way of iterate through lights (split in light types, etc)
	for (uint DirIndex = 0; DirIndex < RaytracingLightsDataPacked.Count; DirIndex++)
	{
		FDeferredLightData LightData = (FDeferredLightData)0;
		uint Lit = 1;

		uint LightType = RaytracingLightsDataPacked.Type_LightProfileIndex_RectLightTextureIndex[DirIndex].x;
		LightData.Position = RaytracingLightsDataPacked.LightPosition_InvRadius[DirIndex].xyz;
		LightData.InvRadius = RaytracingLightsDataPacked.LightPosition_InvRadius[DirIndex].w;
		LightData.Color = RaytracingLightsDataPacked.LightColor_SpecularScale[DirIndex].xyz;
		LightData.FalloffExponent = RaytracingLightsDataPacked.Direction_FalloffExponent[DirIndex].w;
		LightData.Direction = RaytracingLightsDataPacked.Direction_FalloffExponent[DirIndex].xyz;
		LightData.Tangent = RaytracingLightsDataPacked.Tangent_SourceRadius[DirIndex].xyz;
		LightData.SpotAngles = RaytracingLightsDataPacked.SpotAngles_SourceLength_SoftSourceRadius[DirIndex].xy;
		LightData.SourceRadius = RaytracingLightsDataPacked.Tangent_SourceRadius[DirIndex].w;
		LightData.SourceLength = RaytracingLightsDataPacked.SpotAngles_SourceLength_SoftSourceRadius[DirIndex].z;
		LightData.SoftSourceRadius = RaytracingLightsDataPacked.SpotAngles_SourceLength_SoftSourceRadius[DirIndex].w;
		LightData.SpecularScale = RaytracingLightsDataPacked.LightColor_SpecularScale[DirIndex].w;
		LightData.RectLightBarnCosAngle = RaytracingLightsDataPacked.DistanceFadeMAD_RectLightBarnCosAngle_RectLightBarnLength[DirIndex].z;
		LightData.RectLightBarnLength = RaytracingLightsDataPacked.DistanceFadeMAD_RectLightBarnCosAngle_RectLightBarnLength[DirIndex].w;
		LightData.ContactShadowLength = 0.0;
		LightData.DistanceFadeMAD = RaytracingLightsDataPacked.DistanceFadeMAD_RectLightBarnCosAngle_RectLightBarnLength[DirIndex].xy;
		const uint LightProfileIndex = RaytracingLightsDataPacked.Type_LightProfileIndex_RectLightTextureIndex[DirIndex].y;


		LightData.ShadowMapChannelMask = float4(0, 0, 0, 0); // #dxr_todo;
		LightData.ShadowedBits = 0; // Not lit dynamic shadows 
		LightData.ContactShadowLengthInWS = false;

		LightData.bRadialLight = (LightType != LIGHT_TYPE_DIRECTIONAL);
		LightData.bSpotLight = (LightType == LIGHT_TYPE_SPOT);
		LightData.bRectLight = (LightType == LIGHT_TYPE_RECT);

		if (LightType == LIGHT_TYPE_DIRECTIONAL)
		{
			LightData.bInverseSquared = false;
		}
		else
		{
			LightData.bInverseSquared = LightData.FalloffExponent == 0;
		}


		float3 ShadowRayDirection;
		// ToLight should not be normalized because its length is used to compute the shadow ray TMax 
		float3 ToLight = LightData.Position - WorldPosition;
		float LightMask = 1.0;

		if (LightType == LIGHT_TYPE_DIRECTIONAL)
		{
			ShadowRayDirection = LightData.Direction;
			ToLight = LightData.Direction * 100000.0f;
		}
		else
		{
			LightMask = GetLocalLightAttenuation(WorldPosition, LightData, ToLight, ShadowRayDirection);

			// Skip the light sample that does not contribute anything due to attenuation.
			if (LightMask <= 0.0)
			{
				Lit = 0;
			}
		}

		// Skip the light sample pointing backwards
		if (dot(WorldNormal, normalize(ToLight)) <= 0)
		{
			Lit = 0;
		}

		LightCullMask[DirIndex/32] |= Lit << (DirIndex%32);
	}

	return LightCullMask;
}

float3 ComputeDirectLightingCulled(
	in uint2 LightCullMask,
	in RayDesc Ray,
	in FRayCone RayCone,
	in FMaterialClosestHitPayload Payload,
	in RandomSequence RandSequence,
	in bool bReflectedShadows,
	in float ShadowMaxNormalBias)
{
	float3 DirectLighting = float3(0.0, 0.0, 0.0);

	float3 WorldPosition = Payload.WorldPos;
	float3 CameraVector = -Ray.Direction;
	float AmbientOcclusion = 1.0;

	// #dxr_todo: review when interpolants fill the Payload with all the data we need
	FGBufferData GBufferData = (FGBufferData)0;
	GBufferData.Depth = 1.f; // Do not use depth
	GBufferData.WorldNormal = Payload.WorldNormal;
	GBufferData.BaseColor = Payload.BaseColor;
	GBufferData.Metallic = Payload.Metallic;
	GBufferData.Specular = Payload.Specular;
	GBufferData.Roughness = Payload.Roughness;
	GBufferData.CustomData = Payload.CustomData;
	GBufferData.GBufferAO = Payload.GBufferAO;
	//#dxr_todo: change float3 to float in payload to reduce space?;
	GBufferData.IndirectIrradiance = (Payload.IndirectIrradiance.x + Payload.IndirectIrradiance.y + Payload.IndirectIrradiance.z) / 3.f; 
	GBufferData.ShadingModelID = SHADINGMODELID_DEFAULT_LIT;
	GBufferData.SpecularColor = Payload.SpecularColor;
	GBufferData.DiffuseColor = Payload.DiffuseColor;

	FRectTexture RectTexture;
#if USE_SOURCE_TEXTURE_ARRAY 
	RectTexture.SourceTexture0 = RaytracingLightsDataPacked.RectLightTexture0;
	RectTexture.SourceTexture1 = RaytracingLightsDataPacked.RectLightTexture1;
	RectTexture.SourceTexture2 = RaytracingLightsDataPacked.RectLightTexture2;
	RectTexture.SourceTexture3 = RaytracingLightsDataPacked.RectLightTexture3;
	RectTexture.SourceTexture4 = RaytracingLightsDataPacked.RectLightTexture4;
	RectTexture.SourceTexture5 = RaytracingLightsDataPacked.RectLightTexture5;
	RectTexture.SourceTexture6 = RaytracingLightsDataPacked.RectLightTexture6;
	RectTexture.SourceTexture7 = RaytracingLightsDataPacked.RectLightTexture7;
	RectTexture.SourceTextureIndex = 99;
#elif USE_SOURCE_TEXTURE
	RectTexture = InitRectTexture(RaytracingLightsDataPacked.RectLightTexture0);
#else
	RectTexture = (FRectTexture)0;
#endif

	uint DirIndex = 0;

	//#dxr_todo: think on the more efficient way of iterate through lights (split in light types, etc)
	//while(LightCullMask.x || LightCullMask.y)
	while (WaveActiveAnyTrue(LightCullMask.x || LightCullMask.y))
	{
		const bool Active = LightCullMask.x || LightCullMask.y;

		if (Active)
		{
			if ((LightCullMask.x & 0x1) == 0)
			{
				uint Shift = LightCullMask.x ? firstbitlow(LightCullMask.x) : 32;
				DirIndex += Shift;
				LightCullMask.x >>= Shift;

				//fill in the top of LightCullMask.x
				LightCullMask.x |= LightCullMask.y << (32 - Shift);
				LightCullMask.y >>= Shift;

				//may need to try once more
				if ((LightCullMask.x & 0x1) == 0)
				{
					uint Shift = LightCullMask.x ? firstbitlow(LightCullMask.x) : 32;
					DirIndex += Shift;
					LightCullMask.x >>= Shift;

					//fill in the top of LightCullMask.x
					LightCullMask.x |= LightCullMask.y << (32 - Shift);
					LightCullMask.y >>= Shift;
				}
			}
		}
		else
		{
			DirIndex = 0;
		}

		FDeferredLightData LightData = (FDeferredLightData)0;
		uint LightType = RaytracingLightsDataPacked.Type_LightProfileIndex_RectLightTextureIndex[DirIndex].x;
		LightData.Position = RaytracingLightsDataPacked.LightPosition_InvRadius[DirIndex].xyz;
		LightData.InvRadius = RaytracingLightsDataPacked.LightPosition_InvRadius[DirIndex].w;
		LightData.Color = RaytracingLightsDataPacked.LightColor_SpecularScale[DirIndex].xyz;
		LightData.FalloffExponent = RaytracingLightsDataPacked.Direction_FalloffExponent[DirIndex].w;
		LightData.Direction = RaytracingLightsDataPacked.Direction_FalloffExponent[DirIndex].xyz;
		LightData.Tangent = RaytracingLightsDataPacked.Tangent_SourceRadius[DirIndex].xyz;
		LightData.SpotAngles = RaytracingLightsDataPacked.SpotAngles_SourceLength_SoftSourceRadius[DirIndex].xy;
		LightData.SourceRadius = RaytracingLightsDataPacked.Tangent_SourceRadius[DirIndex].w;
		LightData.SourceLength = RaytracingLightsDataPacked.SpotAngles_SourceLength_SoftSourceRadius[DirIndex].z;
		LightData.SoftSourceRadius = RaytracingLightsDataPacked.SpotAngles_SourceLength_SoftSourceRadius[DirIndex].w;
		LightData.SpecularScale = RaytracingLightsDataPacked.LightColor_SpecularScale[DirIndex].w;
		LightData.RectLightBarnCosAngle = RaytracingLightsDataPacked.DistanceFadeMAD_RectLightBarnCosAngle_RectLightBarnLength[DirIndex].z;
		LightData.RectLightBarnLength = RaytracingLightsDataPacked.DistanceFadeMAD_RectLightBarnCosAngle_RectLightBarnLength[DirIndex].w;
		LightData.ContactShadowLength = 0.0;
		LightData.DistanceFadeMAD = RaytracingLightsDataPacked.DistanceFadeMAD_RectLightBarnCosAngle_RectLightBarnLength[DirIndex].xy;

	#if USE_SOURCE_TEXTURE_ARRAY 
		RectTexture.SourceTextureIndex = RaytracingLightsDataPacked.Type_LightProfileIndex_RectLightTextureIndex[DirIndex].z;
	#endif
		uint LightProfileIndex = RaytracingLightsDataPacked.Type_LightProfileIndex_RectLightTextureIndex[DirIndex].y;

		LightData.ShadowMapChannelMask = float4(0, 0, 0, 0); // #dxr_todo;
		LightData.ShadowedBits = 0; // Not lit dynamic shadows 
		LightData.ContactShadowLengthInWS = false;

		LightData.bRadialLight = (LightType != LIGHT_TYPE_DIRECTIONAL);
		LightData.bSpotLight = (LightType == LIGHT_TYPE_SPOT);
		LightData.bRectLight = (LightType == LIGHT_TYPE_RECT);

		if (LightType == LIGHT_TYPE_DIRECTIONAL)
		{
			LightData.bInverseSquared = false;
		}
		else
		{
			LightData.bInverseSquared = LightData.FalloffExponent == 0;
		}
		
		float4 LightAttenuation = 1.0f;

		float LightProfileMultiplier = 1.0;

		if (LightProfileIndex >= 0)
		{
			LightProfileMultiplier = ComputeLightProfileMultiplier(WorldPosition, LightData.Position, LightData.Direction, LightProfileIndex);
		}

		float3 ShadowRayDirection;
		// ToLight should not be normalized because its length is used to compute the shadow ray TMax 
		float3 ToLight = LightData.Position - WorldPosition;
		float LightMask = 1.0;

		if (LightType == LIGHT_TYPE_DIRECTIONAL)
		{
			ShadowRayDirection = LightData.Direction;
			ToLight = LightData.Direction * 100000.0f;
		}
		else
		{
			LightMask = GetLocalLightAttenuation(WorldPosition, LightData, ToLight, ShadowRayDirection);
		}

		//#dxr_todo: check perf improvements forcing TMin == TMax when bReflectedShadows = false, to avoid tracing rays inside if statements
		if (bReflectedShadows)
		{
			if (ReflectedShadowsType == 2)
			{
				ShadowRayDirection = SampleAreaLightDirection(LightData, Payload, LightType, RandSequence);
			}

			FRayCone ShadowRayCone = RayCone;

			//#dxr_todo: add support for sampling area lights (and perhaps add CVar to enable stocastic shadows?)
			RayDesc ShadowRay;
			ShadowRay.Origin = WorldPosition;
			ShadowRay.Direction = ShadowRayDirection;
			ShadowRay.TMin = 1e-4f;
			ShadowRay.TMax = Active ? length(ToLight) : ShadowRay.TMin;
			ApplyPositionBias(ShadowRay, Payload.WorldNormal, ShadowMaxNormalBias);

			float HitT = TraceShadowRay(ShadowRay, ShadowRayCone);
			AmbientOcclusion = HitT < 0.0f && Active;
		}

		if (Active)
		{
			float3 LightContribution = GetDynamicLighting(WorldPosition, -CameraVector, GBufferData, AmbientOcclusion, GBufferData.ShadingModelID, LightData, LightAttenuation, 0.5, uint2(0, 0), RectTexture).xyz;
			DirectLighting += LightContribution * LightProfileMultiplier;
		}

		DirIndex += 1;
		LightCullMask.x >>= 1;

		LightCullMask.x |= LightCullMask.y << 31;
		LightCullMask.y >>= 1;
	}

	return DirectLighting;
}

void ComputeBottomLayerMaterialProperties(RayDesc Ray, inout FMaterialClosestHitPayload Payload)
{
	if (Payload.ShadingModelID == SHADINGMODELID_CLEAR_COAT)
	{
		float ClearCoat				= Payload.CustomData.x;
		float ClearCoatRoughness	= Payload.CustomData.y;
		float MetalSpec = 0.9;

		float NoV = saturate( dot( Payload.WorldNormal, -Ray.Direction ) );

		// Approximation of refraction's effect on EnvBRDF
		float RefractionScale = ( (NoV * 0.5 + 0.5) * NoV - 1 ) * saturate( 1.25 - 1.25 * Payload.Roughness ) + 1;

		// Approximation of absorption integral, tuned for Roughness=0.4
		float3 AbsorptionColor = Payload.BaseColor * (1 / MetalSpec);
		float3 Absorption = AbsorptionColor * ( (NoV - 1) * 0.85 * ( 1 - lerp( AbsorptionColor, Square(AbsorptionColor), -0.78 ) ) + 1 );

		float F0 = 0.04;
		float Fc = Pow5( 1 - NoV );
		float F = Fc + (1 - Fc) * F0;
		float LayerAttenuation = lerp( 1, (1 - F), ClearCoat );

		Payload.BaseColor = lerp( Payload.BaseColor * LayerAttenuation, MetalSpec * Absorption * RefractionScale, Payload.Metallic * ClearCoat );
		Payload.Specular *= lerp( 1, RefractionScale, ClearCoat );
	}
}

FMaterialClosestHitPayload TraceRayResults(
	in RayDesc Ray,
	in RandomSequence RandSequence,
	in uint RayFlags,
	in float ShadowMaxNormalBias,
	in uint Mask,
	inout FRayCone RayCone,
	inout float3 Radiance)
{
	FPackedMaterialClosestHitPayload PackedPayload = TraceRayInternalPacked(
		TLAS,								// AccelerationStructure
		RayFlags,
		Mask,
		RAY_TRACING_SHADER_SLOT_MATERIAL,	// RayContributionToHitGroupIndex
		RAY_TRACING_NUM_SHADER_SLOTS,		// MultiplierForGeometryContributionToShaderIndex
		0,									// MissShaderIndex
		Ray,								// RayDesc
		RayCone
	);
	FMaterialClosestHitPayload Payload = UnpackRayTracingPayload(PackedPayload, Ray);
	return Payload;
}

void AccumulateResults(
	in FMaterialClosestHitPayload Payload,
	in RayDesc Ray,
	in RandomSequence RandSequence,
	in uint RayFlags,
	in float ShadowMaxNormalBias,
	in uint Mask,
	inout FRayCone RayCone,
	inout float3 Radiance)
{
	if (IsHit(Ray, Payload))
	{
		uint2 LightCullMask = (uint2)0;
		const float3 WorldPosition = Ray.Origin + Payload.HitT * Ray.Direction;
		LightCullMask = CullDirectLighting(Ray, RayCone, WorldPosition, Payload.WorldNormal, RandSequence, ReflectedShadowsType > 0, ShadowMaxNormalBias);

		Payload.DiffuseColor = Payload.BaseColor - Payload.BaseColor * Payload.Metallic;
		Payload.SpecularColor = ComputeF0(Payload.Specular, Payload.BaseColor, Payload.Metallic);

		if (ShouldDoDirectLighting)
		{
			float3 DirectLighting = ComputeDirectLightingCulled(LightCullMask, Ray, RayCone, Payload, RandSequence, ReflectedShadowsType > 0, ShadowMaxNormalBias);

			// Transform NaNs to black, transform negative colors to black.
			DirectLighting = -min(-DirectLighting, 0.0);
			Radiance += DirectLighting;
		}

		if (ShouldDoEmissiveAndIndirectLighting)
		{
			// Emissive & indirect contribution
			Radiance += Payload.Radiance;
			Radiance += Payload.DiffuseColor * Payload.IndirectIrradiance;
		}
	}
}

FMaterialClosestHitPayload TraceRayAndAccumulateResults(
	in RayDesc Ray,
	in RandomSequence RandSequence,
	in uint RayFlags,
	in float ShadowMaxNormalBias,
	in uint Mask,
	inout FRayCone RayCone,
	inout float3 Radiance)
{
	FMaterialClosestHitPayload Payload = TraceRayResults(
		Ray,
		RandSequence,
		RayFlags,
		ShadowMaxNormalBias,
		Mask,
		RayCone,
		Radiance);

	AccumulateResults(
		Payload,
		Ray,
		RandSequence,
		RayFlags,
		ShadowMaxNormalBias,
		Mask,
		RayCone,
		Radiance);

	return Payload;
}

FMaterialClosestHitPayload TraceRayAndAccumulateBottomLayerResults(
	in RayDesc Ray,
	in RandomSequence RandSequence,
	in uint RayFlags,
	in float ShadowMaxNormalBias,
	in uint Mask,
	inout FRayCone RayCone,
	inout float3 Radiance)
{
	FMaterialClosestHitPayload BottomLayerPayload = TraceRayResults(
		Ray,
		RandSequence,
		RayFlags,
		ShadowMaxNormalBias,
		Mask,
		RayCone,
		Radiance);

	ComputeBottomLayerMaterialProperties(Ray, BottomLayerPayload);

	AccumulateResults(
		BottomLayerPayload,
		Ray,
		RandSequence,
		RayFlags,
		ShadowMaxNormalBias,
		Mask,
		RayCone,
		Radiance);

	return BottomLayerPayload;
}
