// Copyright 1998-2019 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	SSDCommon.ush: All the code commonly shared between uniform buffers.
=============================================================================*/

#pragma once

#if !defined(SPHERICAL_HARMONIC_ORDER)
	#define SPHERICAL_HARMONIC_ORDER 0
#endif

#include "../Common.ush"
#include "../SceneTextureParameters.ush"
#include "../ColorSpace.ush"
#include "../SphericalGaussian.ush"

#if SPHERICAL_HARMONIC_ORDER > 0
	#include "../SHCommon.ush"
#endif

#include "SSDPublic.ush"
#include "SSDDefinitions.ush"


//------------------------------------------------------- SHARED CONSTANTS DEPENDING ON CONFIG OF SHADER

#if CONFIG_SIGNAL_PROCESSING == SIGNAL_PROCESSING_REFLECTIONS
	#define TARGETED_SAMPLE_COUNT 128

#elif CONFIG_SIGNAL_PROCESSING == SIGNAL_PROCESSING_AO || CONFIG_SIGNAL_PROCESSING == SIGNAL_PROCESSING_MONOCHROMATIC_PENUMBRA
	// A gray scale valued encoded into a 16bit float only have 10bits mantissa.
	#define TARGETED_SAMPLE_COUNT 1024

#elif CONFIG_SIGNAL_PROCESSING == SIGNAL_PROCESSING_DIFFUSE_INDIRECT_AND_AO
	// Indirect diffuse requires really large spatial kernel to clean the noise.
	#define TARGETED_SAMPLE_COUNT 4096

#else
	#error Unconfigured TARGETED_SAMPLE_COUNT

#endif


//------------------------------------------------------- SHARED SHADER PARAMETERS

float4x4  PrevScreenToTranslatedWorld;
Texture2D PrevDepthBuffer;
Texture2D PrevGBufferA;
Texture2D PrevGBufferB;


//------------------------------------------------------- SHARED FUNCTION

float SafeRcp(float x)
{
	if (x > 0)
		return rcp(x);
	return 0;
}

//------------------------------------------------------- SPHERICAL HARMONIC INTERFACE

#if SPHERICAL_HARMONIC_ORDER == 0
	// NOP

#elif SPHERICAL_HARMONIC_ORDER == 2
typedef FTwoBandSHVector FSSDSphericalHarmonic;
typedef FTwoBandSHVectorRGB FSSDSphericalHarmonicRGB;

#elif SPHERICAL_HARMONIC_ORDER == 3
typedef FThreeBandSHVector FSSDSphericalHarmonic;
typedef FThreeBandSHVectorRGB FSSDSphericalHarmonicRGB;

#else
	#error Unknown spherical harmonic configuration.

#endif


//------------------------------------------------------- COMMON SCENE INFOS

/** olds commonly used information of the scene of a given sample. */
struct FSSDSampleSceneInfos
{
	/** Normal of the pixel in world space. */
	float3 WorldNormal;

	/** The position of the pixel in the translated world frame. */
	float3 TranslatedWorldPosition;

	/** Pixel depth in world space. */
	float WorldDepth;

	/** Roughness of the pixel. */
	float Roughness;
};

FSSDSampleSceneInfos CreateSampleSceneInfos()
{
	FSSDSampleSceneInfos Infos;
	Infos.WorldNormal             = 0;
	Infos.TranslatedWorldPosition = 0;
	Infos.WorldDepth              = 0;
	Infos.Roughness               = 0;
	return Infos;
}

FSSDSampleSceneInfos InitSampleSceneInfos(
	float4x4 ScreenToTranslatedWorld,
	float2 ScreenPosition, float WorldDepth, float3 WorldNormal, float Roughness)
{
	FSSDSampleSceneInfos Infos;

	float2 ClipPosition = ScreenPosition * (View.ViewToClip[3][3] < 1.0f ? WorldDepth : 1.0f);
	Infos.TranslatedWorldPosition = mul(float4(ClipPosition, WorldDepth, 1), ScreenToTranslatedWorld).xyz;
	Infos.WorldDepth = WorldDepth;
	Infos.WorldNormal = WorldNormal;
	Infos.Roughness = Roughness;
	return Infos;
}

FSSDSampleSceneInfos FetchCurrentSceneInfos(float2 ScreenPosition, float2 BufferUV)
{
	FGBufferData GBufferData = GetGBufferDataFromSceneTextures(BufferUV);
	return InitSampleSceneInfos(
		View.ScreenToTranslatedWorld,
		ScreenPosition, GBufferData.Depth, GBufferData.WorldNormal, GBufferData.Roughness);
}

FSSDSampleSceneInfos FetchPrevSceneInfos(float2 ScreenPosition, float2 BufferUV)
{
	// TODO: so what to do with mip level of these guys?
	float WorldSceneDepth = ConvertFromDeviceZ(PrevDepthBuffer.SampleLevel(GlobalPointClampedSampler, BufferUV, 0).r);
	float3 WorldNormal = DecodeNormal(PrevGBufferA.SampleLevel(GlobalPointClampedSampler, BufferUV, 0).rgb);
	float Roughness = PrevGBufferB.SampleLevel(GlobalPointClampedSampler, BufferUV, 0).b;

	FSSDSampleSceneInfos OutMetadata;
	OutMetadata = InitSampleSceneInfos(
		View.PrevScreenToTranslatedWorld,
		ScreenPosition, WorldSceneDepth, WorldNormal, Roughness);

	// TODO: PrevScreenToCurrentTranslatedWorld
	OutMetadata.TranslatedWorldPosition += -View.PrevPreViewTranslation.xyz + View.PreViewTranslation.xyz;
	return OutMetadata;
}

float ProjectWorldDistanceToScreenDistance(FSSDSampleSceneInfos SceneMetadata, float WorldDistance)
{
	float4 ClipDistance = mul(View.ViewToClipNoAA, float4(WorldDistance, 0, SceneMetadata.WorldDepth, 1));
	return ClipDistance.x / ClipDistance.w;
}

float ProjectWorldDistanceToPixelDistance(FSSDSampleSceneInfos SceneMetadata, float WorldDistance)
{
	return WorldDistance * View.ViewSizeAndInvSize.x * 0.5 / (SceneMetadata.WorldDepth * GetTanHalfFieldOfView().x);
}

/** Compute the anisotropy value. The lower the value is, the more anysotropic the sample is. */
float ComputeAnisotropyInvFactor(FSSDSampleSceneInfos SceneMetadata)
{
	if (!CONFIG_NORMAL_ANISOTROPY)
	{
		return 1;
	}

	float Anysotropy = abs(dot(SceneMetadata.WorldNormal, View.ViewForward));

	return max(Anysotropy, rcp(8));
}

/** Return whether this signal should store in given mip level. */
float ComputeSignalMipLevel(FSSDSampleSceneInfos SceneMetadata, float WorldBluringRadius)
{
	if (WorldBluringRadius == WORLD_RADIUS_INVALID)
	{
		return MIP_LEVEL_INVALID;
	}
	else if (WorldBluringRadius == WORLD_RADIUS_MISS)
	{
		return MIP_LEVEL_MISS;
	}
	else if (DEBUG_MIP_LEVEL != MIP_LEVEL_INVALID)
	{
		return DEBUG_MIP_LEVEL;
	}

	// Compute how much the pixel can be blured on screen
	// TODO: anysotopy with scene bent normals?
	float SmallestPixelBlurRadius = ProjectWorldDistanceToPixelDistance(SceneMetadata, WorldBluringRadius);

	// Take into account the spatial bluring radius that may also happen when not enough temporal samples.
	SmallestPixelBlurRadius *= rcp(BLURING_ESTIMATION_MULTIPLIER);

	// Compute on what mip level this signal can be denoised.
	return clamp(log2(SmallestPixelBlurRadius), 0, MAX_MIP_LEVEL_COUNT - 1);
}

/** Returns the radius of a pixel in world space. */
// TODO: move that to CPU
float ComputeWorldBluringRadiusCausedByPixelSize(FSSDSampleSceneInfos SceneMetadata)
{
	// Should be multiplied 0.5* for the diameter to radius, and by 2.0 because GetTanHalfFieldOfView() cover only half of the pixels.
	return (View.ViewSizeAndInvSize.z * GetTanHalfFieldOfView().x) * SceneMetadata.WorldDepth;
}

/** Convert to bilateral world distance. */
float WorldBluringRadiusToBilateralWorldDistance(float WorldBluringRadius)
{
	float Multiplier = 1;

	// The distance between two pixel is 2 times the radius of the pixel.
	Multiplier *= 2;

	// Need to take into account the furthearest pixel of 3x3 neighborhood.
	Multiplier *= sqrt(2);

	// Can take into account how much the signal is getting stored into higher resolution.
	Multiplier *= BLURING_ESTIMATION_MULTIPLIER;

	return WorldBluringRadius * Multiplier;
}
